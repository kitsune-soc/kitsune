post = _{ 
    (text | component)* 
}

component = _{ 
    emote | hashtag | mention 
}

text = { 
    (!component ~ ANY)+ 
}

component_prefix = { 
    (NEWLINE | SOI | WHITE_SPACE)
}

component_postfix = { 
    &(EOI | WHITE_SPACE)
}

emote = { 
    ":" 
    ~ emote_content 
    ~ ":" 
}

emote_content = {
    (LETTER | NUMBER | DASH_PUNCTUATION)+
}

hashtag = { 
    component_prefix 
    ~ "#" 
    ~ hashtag_content 
    ~ (component_postfix | !hashtag_content) 
}

hashtag_content = {
    (
        !component_postfix 
        ~ (LETTER | NUMBER)+
    )+ 
}

mention = {
    component_prefix 
    ~ "@" 
    ~ mention_username 
    ~ ("@" ~ mention_domain)? 
    ~ component_postfix 
}

mention_domain = {
    (
        !(component_postfix | ".") ~ ANY
    )+ 
    ~ "." 
    ~ (
        !component_postfix ~ ANY
    )+ 
}

mention_username = { 
    (
        !(component_postfix | "@") 
        ~ ANY
    )+ 
}
